plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.github.node-gradle.node' version '7.0.2'
}

group = 'co.edu.uniquindio'
version = '0.0.1-SNAPSHOT'

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

// CONFIGURACION DEL FRONTEND
node {
    version = '20.11.0'
    npmVersion = '10.2.4'
    download = true
    workDir = file("${project.projectDir}/.gradle/nodejs")
    nodeProjectDir = file("${project.projectDir}/frontend")
}

// Construir el frontend
tasks.register('buildFrontend', com.github.gradle.node.npm.task.NpmTask) {
    dependsOn 'npm_install'
    args = ['run', 'build']
    inputs.dir('frontend/src')
    inputs.dir('frontend/public')
    inputs.file('frontend/package.json')
    outputs.dir('frontend/build')
}

// Copiar el build del frontend a resources/static
tasks.register('copyFrontend', Copy) {
    dependsOn 'buildFrontend'
    from 'frontend/build'
    into 'src/main/resources/static'
}

// Limpiar archivos del frontend
tasks.register('cleanFrontend', Delete) {
    delete 'frontend/build'
    delete 'src/main/resources/static'
}

// Integrar la construccion del frontend con el proceso de compilacion
processResources {
    dependsOn 'copyFrontend'
}

// Integrar la limpieza del frontend con clean
clean {
    dependsOn 'cleanFrontend'
}